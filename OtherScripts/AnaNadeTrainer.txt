settings
{
	main
	{
		Description: "Learn Ana set-nade spots on Eichenwalde, Route66, Anubis, Hanamura, Dorado, Hollywood, Blz World, Kings Row. Code 2Y555. Created by WelkinTern"
	}

	lobby
	{
		Max Team 1 Players: 0
		Max Team 2 Players: 12
	}

	modes
	{
		Assault
		{
			enabled maps
			{
			}
		}

		Control
		{
			enabled maps
			{
			}
		}

		Escort
		{
			enabled maps
			{
				Dorado
			}
		}

		Hybrid
		{
			enabled maps
			{
			}
		}

		General
		{
			Hero Limit: Off
		}
	}

	heroes
	{
		General
		{
			Ability Cooldown Time: 0%
			No Ammunition Requirement: On
			Ultimate Generation - Combat: 0%
			Ultimate Generation - Passive: 0%

			enabled heroes
			{
				Ana
			}
		}
	}
}

variables
{
	global:
		0: CPLocation
		1: CPDestination
		2: CPFacing
		3: CPHintText
		4: CPHintEffectPosition
		5: CPScoped
		6: CPObjectiveText
		13: DevModeEngaged
		15: PioneerMode

	player:
		0: CP
		1: ChaseCamOn
		5: StartPos
		6: StartVelocity
		7: NadeTimerChase
		10: AnaSleepDartTeleLocation
		11: Destination
}

subroutines
{
	0: RespawnPlayer
}

rule("Initialize Globals and HUD")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Disable Game Mode HUD(All Players(All Teams));
		Global.DevModeEngaged = False;
		Global.PioneerMode = False;
		Create HUD Text(All Players(All Teams), Null, Null, Custom String(
			"Ana Nade Trainer By WelkinTern | Code 2Y555\r\nMaps: Eich, Route66, Anubis, Hana, Dorado, \r\n      Holly, Blz"), Left, -1,
			Color(White), Color(White), Color(Green), Visible To, Default Visibility);
		Create HUD Text(All Players(All Teams), Null, Null, Custom String(
			"- - - - - - - - - Controls - - - - - - - - - \r\n{0}      | Reset Camera + Position\r\n{1}\r\n{2}", Input Binding String(
			Button(Reload)), Custom String("{0} + {1} | Next Checkpoint\r\n{2}    | Reset Camera Only", Input Binding String(Button(
			Interact)), Input Binding String(Button(Reload)), Input Binding String(Button(Melee))), Custom String(
			"{0}    | Teleport (Like Ana Parkour)\r\n{1}", Ability Icon String(Hero(Ana), Button(Ability 1)), Custom String(
			"{0} + {1} | Toggle Chase Cam", Input Binding String(Button(Melee)), Input Binding String(Button(Ultimate))))), Left, 0, Color(
			White), Color(White), Color(Orange), Visible To and String, Default Visibility);
		Create HUD Text(All Players(All Teams), Null, Null, Custom String(
			"- - - - - - - - - - - - - - - - - - -\r\nUlt Status | Set Throw Destination\r\n{0} + {1} | Calculate Throw Angle To Destination",
			Input Binding String(Button(Interact)), Input Binding String(Button(Ultimate))), Left, 1, Color(White), Color(White), Color(
			Orange), Visible To and String, Default Visibility);
		Create HUD Text(Filtered Array(Host Player, Global.DevModeEngaged), Null, Null, Custom String("Position: {0}\r\n{1}\r\n{2}",
			Position Of(Local Player), Custom String("Facing Vector: {0}\r\nFacing Angles: H{1}, V{2}", Facing Direction Of(Local Player),
			Horizontal Angle From Direction(Facing Direction Of(Local Player)), Vertical Angle From Direction(Facing Direction Of(
			Local Player))), Custom String("Ray Cast: {0}\r\nCollision Position: {1}", Ray Cast Hit Position(Eye Position(Local Player),
			Eye Position(Local Player) + 180 * Facing Direction Of(Local Player), Null, Null, False),
			Local Player.StartPos + Local Player.StartVelocity * Local Player.NadeTimerChase + Vector(0, -4.900, 0)
			* Local Player.NadeTimerChase * Local Player.NadeTimerChase)), Left, 9, Color(White), Color(White), Color(White),
			Visible To and String, Default Visibility);
		Create HUD Text(All Players(All Teams), Null, Null, Custom String("Checkpoint {0} / {1}\r\nFly Time: {2}", Local Player.CP,
			Count Of(Global.CPLocation) - 1, Local Player.NadeTimerChase), Right, 5, Color(White), Color(White), Color(White),
			Visible To and String, Default Visibility);
		"Give map data a time to load"
		Wait(0.100, Ignore Condition);
		"If no map data for this map, initialize CP vars as empty arrays"
		If(Count Of(Global.CPLocation) == 0);
			Global.CPLocation = Empty Array;
			Global.CPDestination = Empty Array;
			Global.CPFacing = Empty Array;
			Global.CPHintText = Empty Array;
			Global.CPHintEffectPosition = Empty Array;
			Global.CPScoped = Empty Array;
			Global.CPObjectiveText = Empty Array;
		End;
	}
}

rule("Initialize Player")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	actions
	{
		Disable Game Mode HUD(Event Player);
		Event Player.ChaseCamOn = True;
		Event Player.Destination = Null;
		Event Player.CP = 0;
		"Orb tracking projectile position"
		Create Effect(Filtered Array(Event Player, Current Array Element.NadeTimerChase > 0), Sphere, Color(Red),
			Event Player.StartPos + Event Player.StartVelocity * Event Player.NadeTimerChase + Vector(0, -4.900, 0)
			* Event Player.NadeTimerChase * Event Player.NadeTimerChase, 1, Visible To Position and Radius);
		"Create Ring Effects"
		Create Effect(Event Player, Ring, Color(White), Global.CPLocation[Event Player.CP], 2, Visible To Position and Radius);
		Create Effect(Event Player, Ring, Color(White), Global.CPDestination[Event Player.CP], 4, Visible To Position and Radius);
		"Objective and Hint Text"
		Create HUD Text(Event Player, Custom String("Objective: {0}\r\nAim Hint: {1}", Global.CPObjectiveText[Event Player.CP],
			Global.CPHintText[Event Player.CP]), Null, Null, Top, 0, Color(White), Color(White), Color(White), Visible To and String,
			Default Visibility);
		"Hint Orb"
		Create Effect(Event Player, Sphere, Custom Color(128 + 127 * Cosine From Degrees(Total Time Elapsed * 100),
			128 + 127 * Cosine From Degrees(120 + Total Time Elapsed * 100), 128 + 127 * Cosine From Degrees(
			240 + Total Time Elapsed * 100), 255), Global.CPHintEffectPosition[Event Player.CP], Distance Between(
			Global.CPLocation[Event Player.CP], Global.CPHintEffectPosition[Event Player.CP]) * Sine From Degrees(1),
			Visible To Position Radius and Color);
		"Flag icon for destination"
		Create Icon(Filtered Array(Event Player, Event Player.Destination != Null), Event Player.Destination, Flag,
			Visible To and Position, Color(Yellow), False);
		Set Status(Event Player, Null, Phased Out, 9999);
		Create Effect(Filtered Array(Event Player, Event Player.Destination != Null), Ring, Color(Purple), Event Player.Destination, 4,
			Visible To Position and Radius);
	}
}

disabled rule("Map Data")
{
	event
	{
		Ongoing - Global;
	}
}

rule("Eichenwalde Map Data")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Current Map == Map(Eichenwalde);
	}

	actions
	{
		Global.CPLocation = Array(Vector(26.200, 5.350, -67.080), Vector(-3.340, 2.290, -22.710), Vector(22.540, 3.520, -23.920), Vector(
			-13.190, 2.550, -31.750), Vector(-13.190, 2.550, -31.750), Vector(18.149, 4.870, -43.419), Vector(21.913, 5.320, -66.216),
			Vector(27.826, 5, -45.516), Vector(22.810, 5, -52.176), Vector(-2.100, 3.700, -80.440), Vector(88.287, 12.770, -101.982),
			Vector(94.380, 13.780, -67.391), Vector(98.544, 10.750, -27.622));
		Global.CPDestination = Array(Vector(-11.940, 0, -9.960), Vector(22, 5.440, -61.400), Vector(20.780, 5.790, -63.470), Vector(17.070,
			5.460, -48.340), Vector(10.970, 10.470, -40.660), Vector(16.530, 6.800, -76.050), Vector(51.210, 22.520, -64.860), Vector(
			54.450, 18.890, -78.470), Vector(54.460, 18.310, -102.480), Vector(81.100, 13.180, -73.580), Vector(16.700, 6.460, -75.120),
			Vector(-5.150, 3.920, -84.340), Vector(79.190, 12.930, -98.030));
		Global.CPFacing = Array(Vector(-0.220, 0.920, 0.330), Vector(0.144, 0.960, -0.220), Vector(-0.010, 0.970, -0.220), Vector(0.170,
			0.980, -0.090), Vector(0.140, 0.990, -0.050), Vector(-0.010, 0.980, -0.180), Vector(0.180, 0.984, 0.010), Vector(0.170, 0.970,
			-0.200), Vector(0.200, 0.920, -0.330), Vector(0.630, 0.780, 0.050), Vector(-0.420, 0.890, 0.160), Vector(-0.810, 0.570,
			-0.140), Vector(-0.230, 0.460, -0.860));
		Global.CPObjectiveText = Array(Custom String("Hit Enemy Team Coming Out of Spawn At Beginning of Round"), Custom String(
			"Hit a big anti on enemy team to initiate near 1st point"), Custom String(
			"Hit a big anti on enemy team to initiate near 1st point"), Custom String(
			"Anti their tanks if they're playing close to the bridge"), Custom String("Anti people being annoying on the bridge"),
			Custom String("Hit a big anti on enemy team on point"), Custom String("Anti people on the top of the castle"), Custom String(
			"Hit enemies on top of the castle"), Custom String("Another Anti-Sniper Spot"), Custom String(
			"Throw nade to end of 2nd point when respawning"), Custom String("Throw nade from spawn to 1st point"), Custom String(
			"Hit enemies coming out of attack spawn"), Custom String("Throw nade from spawn to middle 2nd point"));
		Global.CPHintText = Array(Custom String("Align top-right corner of triangle scope with tower corner"), Custom String(
			"Aim for the metal bracket on the post in front of you"), Custom String("Aim for the tip of the tree leaf directly above"),
			Custom String(
			"Scope, then put the center of your ultimate meter \r\nwhere the post intersects the roof (over the green sphere)"),
			Custom String("Scope, then put the center of your ultimate meter \r\non the corner of the roof (over the green sphere)"),
			Custom String("Aim at the corner of the roof above"), Custom String(
			"Put your crossheir on the corner with the green sphere\r\nthen move slightly to the right"), Custom String(
			"Scope, then align bottom left \"wick\" with the tower corner"), Custom String(
			"Scope, then put crossheir touching the branch tip. \r\nOr put the left side of your ultimate meter on tower spire"),
			Custom String("Put your crossheir on the flat part of stone pillar, then jump and throw"), Custom String(
			"When walking out of spawn, aim at approx this height"), Custom String("Aim slightly above the left side of the tree"),
			Custom String("Stand on carpet corner, aim for center of archway stone"));
		Global.CPHintEffectPosition = Array(Vector(17.640, 36.060, -57.940), Null, Null, Vector(-10.680, 11.080, -33.100), Vector(-10.810,
			11.330, -32.620), Null, Vector(22.720, 10.950, -66.240), Vector(31.780, 23.250, -52.730), Null, Null, Null, Null, Null);
		Global.CPScoped = Array(True, False, True, True, True, False, True, True, True, False, False, False, False);
	}
}

rule("Route66 Map Data")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Current Map == Map(Route 66);
	}

	actions
	{
		Global.CPLocation = Array(Vector(33.305, 0.230, 41.559), Vector(33.305, 0.230, 41.559), Vector(53.665, 2.450, -8.287), Vector(
			13.301, 8.470, -21.098), Vector(-20.977, 3.500, -41.030), Vector(-41.762, 4, -24.120));
		Global.CPDestination = Array(Vector(36.140, 8.820, -2.440), Vector(47.050, 2.950, 4.500), Vector(44, 3.100, 61.840), Vector(
			-27.350, 9, -29.250), Vector(21.800, 2.800, 7.600), Vector(3, 809, -44.700));
		Global.CPFacing = Array(Vector(0.020, 0.970, -0.260), Vector(0.080, 0.970, -0.210), Vector(-0.060, 0.910, 0.420), Vector(-0.220,
			0.970, -0.040), Vector(0.250, 0.930, 0.280), Vector(0.850, 0.340, -0.390));
		Global.CPObjectiveText = Array(Custom String("Nade enemy team on top of gas station"), Custom String(
			"Nade enemy tanks holding 1st choke"), Custom String("Nade enemy team coming out of spawn"), Custom String(
			"Hit enemies on the common stairs location"), Custom String("Throw nade when walking from spawn to 1st point"), Custom String(
			"Hit enemies in the cave"));
		Global.CPHintText = Array(Custom String("Align top left of triangle crossheir on corner of metal piece"), Custom String(
			"Align upper right \"wick\" of scope with the orb at end of metal piece"), Null, Custom String(
			"Walk into the corner, then aim for the top of the wood branch"), Custom String(
			"Scope, then put your ultimate meter on top the metal pipe"), Custom String(
			"Walk on bottom of stair railing, look at top of railing, jump and throw"));
		Global.CPHintEffectPosition = Array(Vector(32.850, 13.950, 38.650), Vector(38.700, 30.270, 39.740), Null, Vector(-27.460, 184.850,
			-29.270), Null, Null);
		Global.CPScoped = Array(True, True, True, True, False, True);
	}
}

rule("Temple of Anubis Map Data")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Current Map == Map(Temple of Anubis);
	}

	actions
	{
		Global.CPLocation = Array(Vector(-18.870, 1, -0.925), Vector(-25.758, 4.550, 30.828));
		Global.CPDestination = Array(Vector(-22, 179.200, 32.500), Vector(1.930, 0.640, -17.500));
		Global.CPFacing = Array(Vector(-0.020, 0.980, 0.180), Vector(0.150, 0.950, -0.270));
		Global.CPObjectiveText = Array(Custom String("Anti their whole team to start the round :D"), Custom String(
			"Anti their whole team to start the round :D"));
		Global.CPHintText = Array(Custom String("Scope, then align your ultimate meter with the top light wall section"), Custom String(
			"Scope then center your ult meter on the spire (then move up slightly)"));
		Global.CPHintEffectPosition = Array(Null, Null);
		Global.CPScoped = Array(True, True);
	}
}

rule("Hanamura Map Data")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Array Contains(Array(Map(Hanamura), Map(Hanamura Winter)), Current Map) == True;
	}

	actions
	{
		Global.CPLocation = Array(Vector(29.116, 1.425, 12.736), Vector(33.699, 0.250, 14.862), Vector(0.631, 0, -16.893), Vector(13.398,
			0, -23.642));
		Global.CPDestination = Array(Vector(26.200, 0.900, 0.960), Vector(-4.420, 1.710, -18.650), Vector(72.750, -1.300, 0.700), Vector(
			-9, -0.500, -84));
		"Using X = horizontal angle, Y = Vertical Angle"
		Global.CPFacing = Array(Vector(-166.400, -86.210, 0), Vector(-131.320, -73.210, 0), Vector(76.280, -63.690, 0), Vector(-159.710,
			-20.320, 0));
		Global.CPObjectiveText = Array(Custom String("Anti their tanks at choke"), Custom String("Anti their supports"), Custom String(
			"Anti their team coming out of spawn"), Custom String("Nade the point when walking back to the fight from spawn"));
		Global.CPHintText = Array(Custom String("Scope, then align your crossheir against the wall near vertical"), Custom String(
			"Scope, then align right side of crossheir where the 2 wood pieces intersect"), Custom String(
			"Scope, then align the white part on the right with the top of roof corner"), Custom String(
			"When walking under the gate, aim for the top of the tree in center"));
		Global.CPHintEffectPosition = Array(Null, Null, Null, Null);
		Global.CPScoped = Array(True, True, True, False);
	}
}

rule("Dorado Map Data")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Current Map == Map(Dorado);
	}

	actions
	{
		Global.CPLocation = Array(Vector(5.341, 0, 12.674), Vector(28.179, 1, 10.147), Vector(32.479, 8, -50.431), Vector(84.500, 9.200,
			-24.687), Vector(104.254, 9, -20.376), Vector(128.270, 11, 15.204), Vector(169.118, 11, 29.089));
		Global.CPDestination = Array(Vector(28.600, 12, -26.500), Vector(50, 7.500, -19), Vector(-2.340, 0, 5.640), Vector(107.600, 18.600,
			-3.300), Vector(125, 12, 21.600), Vector(116.800, 8.550, -2.650), Vector(178, 12.740, 56.030));
		"Using X = horizontal angle, Y = Vertical Angle"
		Global.CPFacing = Array(Vector(149.240, -73.930, 0), Vector(143.110, -77.960, 0), Vector(-31.820, -68.480, 0), Vector(47.270,
			-79.430, 0), Vector(26.460, -74.490, 0), Vector(-147.410, -83.320, 0), Vector(18.370, -80.970, 0));
		Global.CPObjectiveText = Array(Custom String("Anti their team at start of round"), Custom String(
			"Anti their team holding the choke under bridge"), Custom String("Anti their team at start of round"), Custom String(
			"Hit enemies on close high ground"), Custom String("Hit their tanks holding the bridge"), Custom String(
			"Hit their tanks holding the bridge"), Custom String("Hit their tanks holding the bridge"));
		Global.CPHintText = Array(Custom String("Scope, then align the white part of crossheir on right with building corner"),
			Custom String("Align the center of your ultimate meter with the corner \r\nof building under roof (where the orb is)"),
			Custom String("Scope, then align the white part of crossheir on right with\r\nwhere stone pillar hits the roof"),
			Custom String("Look up at the roof corner"), Custom String("Look up at the cylinder (electrical thing)"), Custom String(
			"Look right above corner of metal"), Custom String("Look up at center of roof panel where it changes color"));
		Global.CPHintEffectPosition = Array(Vector(8.160, 11.400, 10.630), Vector(32.500, 11.500, 4.600), Null, Null, Null, Null, Null,
			Null);
		Global.CPScoped = Array(True, False, True, False, False, True, True);
	}
}

rule("Hollywood Map Data")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Array Contains(Array(Map(Hollywood), Map(Hollywood Halloween)), Current Map) == True;
	}

	actions
	{
		Global.CPLocation = Array(Vector(7.821, 2, 30.474), Vector(-31.396, 0.800, 19.828), Vector(-15.704, 1.750, -4.276), Vector(-31.441,
			1.750, -8.973), Vector(-44.155, -0.250, -6.348), Vector(25.845, 1.720, -30.776));
		Global.CPDestination = Array(Vector(-18, 2.300, 5.300), Vector(-11.700, 1.720, 70), Vector(3.570, 11.400, -30.700), Vector(11.190,
			2.200, -77.500), Vector(-5.700, 2.600, -20), Vector(-26.370, 3.700, -2.300));
		Global.CPFacing = Array(Vector(-134.100, -78.500, 0), Vector(21.470, -72.100, 0), Vector(143.670, -79.100, 0), Vector(148.120,
			59.570, 0), Vector(109.780, -76.670, 0), Vector(-61.400, -69.870, 0));
		Global.CPObjectiveText = Array(Custom String("Anti their tanks holding the point"), Custom String(
			"Hit their team coming out of spawn"), Custom String("Hit enemies on top right building"), Custom String(
			"Throw nade coming back to fight on 2nd point"), Custom String("Hit enemy tanks"), Custom String(
			"Throw nade to 1st point when returning to fight"));
		Global.CPHintText = Array(Custom String("Scope, then align right white part of crossheir with top of wooden part of tree"),
			Custom String("Scope then center your ult meter where the roofs meet"), Custom String("Look 2 tiles up from corner of roofs"),
			Custom String("Press R, its pretty obvious"), Null, Custom String("Aim at top of box, above center of left wood panel"));
		Global.CPHintEffectPosition = Array(Null, Vector(-25, 18.720, 34.320), Null, Null, Null, Null);
		Global.CPScoped = Array(True, False, False, False, True, False);
	}
}

rule("Blz World Map Data")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Array Contains(Array(Map(Blizzard World), Map(Black Forest Winter)), Current Map) == True;
	}

	actions
	{
		Global.CPLocation = Array(Vector(-5.505, -1, 5.647), Vector(11.922, 0, 46.034), Vector(3.103, 8, 81.280), Vector(-72.023, 0.800,
			98.577));
		Global.CPDestination = Array(Vector(-6, 6.700, 52), Vector(8.500, 2.700, 71), Vector(2.600, -3.300, -18.360), Vector(1.150, 2.600,
			75.330));
		Global.CPFacing = Array(Vector(-0.790, -73.970, 0), Vector(-7.820, -81.970, 0), Vector(-179.770, -53, 0), Vector(107.640, -61.640,
			0));
		Global.CPObjectiveText = Array(Custom String("Hit enemies on the platform overlooking route to 1st point"), Custom String(
			"Anti their tanks on point"), Custom String("Anti their whole team to start the round :D"), Custom String(
			"Throw nade to 1st point while returning from spawn"));
		Global.CPHintText = Array(Custom String("Put ultimate meter on the top of the bird statue"), Custom String(
			"Scope, then put bottom of blue rows of crossheir on top wooden beam"), Custom String(
			"Stand in middle of boat, then center ult meter on top archway roof"), Custom String(
			"Look above pylon at same angle as the sun"));
		Global.CPHintEffectPosition = Array(Null, Null, Null, Null);
		Global.CPScoped = Array(False, True, False, False);
	}
}

rule("Kings Row Map Data")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Array Contains(Array(Map(King's Row), Map(King's Row Winter)), Current Map) == True;
	}

	actions
	{
		Global.CPLocation = Array(Vector(-8.677, 0.072, -10.312), Vector(2.948, 0.055, -11.892), Vector(9.119, 0.065, -21.753), Vector(
			9.119, 0.065, -21.753), Vector(-7.571, 0.022, -25.400), Vector(-49.184, 0.163, -38.250), Vector(-20.939, 0.118, -40.889));
		Global.CPDestination = Array(Vector(-15.829, 0.595, -28.464), Vector(-9.329, 6.944, -40.993), Vector(-28.017, 0.580, -23.816),
			Vector(-31.773, 9.750, -34.063), Vector(-60.923, 0.485, -32.162), Vector(-89.112, 1.383, -20.409), Vector(-3.845, 0.972,
			16.273));
		Global.CPFacing = Array(Vector(-158.494, -83.892, 0), Vector(-157.126, -79.585, 0), Vector(-93.181, -78.107, 0), Vector(-106.754,
			-75.317, 0), Vector(-97.224, -72.175, 0), Vector(-65.923, -75.784, 0), Vector(16.650, -69.802, 0));
		Global.CPHintText = Array(Custom String("Scope and put triangle crossheir above dark section of wall"), Custom String(
			"Look very slightly above crack in wall"), Custom String("Look at corner above"), Custom String(
			"Scope, align upper right triangle crossheir with corner, \r\nthen move up slightly"), Custom String(
			"Align lower left triangle crossheir corner with corner between 2 roofs"), Custom String(
			"Scope, align upper left triangle crossheir with smallest metal piece"), Custom String(
			"Align triangle crossheir with metal piece"));
		Global.CPHintEffectPosition = Array(0, 0, 0, 0, 0, 0, 0);
		Global.CPScoped = Array(True, True, False, True, True, True, True);
		Global.CPObjectiveText = Array(Custom String("Anti Their Tanks On 1st Point"), Custom String(
			"Hit DPS on the perch above 1st point"), Custom String("Hit enemy team behind 1st point"), Custom String(
			"Hit the Widow Players"), Custom String("Hit tanks in common position"), Custom String("Hit tanks in common position"),
			Custom String("Hit enemy team coming out of spawn"));
		Global.DevModeEngaged = True;
		Global.PioneerMode = False;
	}
}

disabled rule("Game Framework")
{
	event
	{
		Ongoing - Global;
	}
}

rule("Teleport player at first cp when they spawn")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Has Spawned(Event Player) == True;
		Count Of(Global.CPLocation) > 0;
	}

	actions
	{
		Call Subroutine(RespawnPlayer);
	}
}

rule("Respawn Player on CP if they die")
{
	event
	{
		Player Died;
		All;
		All;
	}

	actions
	{
		Respawn(Event Player);
		Wait(0.100, Ignore Condition);
		Call Subroutine(RespawnPlayer);
	}
}

disabled rule("Trajectory And Camera Stuff")
{
	event
	{
		Ongoing - Global;
	}
}

rule("Throw Nade")
{
	event
	{
		Ongoing - Each Player;
		All;
		Ana;
	}

	conditions
	{
		Is Using Ability 2(Event Player) == True;
	}

	actions
	{
		Stop Camera(Event Player);
		Event Player.NadeTimerChase = 0;
		Chase Player Variable At Rate(Event Player, NadeTimerChase, 10, 1, Destination and Rate);
		"should this be eye position?"
		Event Player.StartPos = Eye Position(Event Player);
		Event Player.StartVelocity = 30 * Facing Direction Of(Event Player);
		If(Angle Between Vectors(Facing Direction Of(Event Player), Up) < 0.100);
			Event Player.StartVelocity = Vector(0, 30, 0);
		End;
		If(Event Player.ChaseCamOn);
			Start Camera(Event Player, Event Player.StartPos + Event Player.StartVelocity * Event Player.NadeTimerChase + Vector(0, -4.900, 0)
				* Event Player.NadeTimerChase * Event Player.NadeTimerChase + -8 * Vector(X Component Of(Facing Direction Of(Event Player)),
				-1 * Absolute Value(Y Component Of(Facing Direction Of(Event Player))), Z Component Of(Facing Direction Of(Event Player))),
				Event Player.StartPos + Event Player.StartVelocity * Event Player.NadeTimerChase + Vector(0, -4.900, 0)
				* Event Player.NadeTimerChase * Event Player.NadeTimerChase, 30);
		End;
	}
}

rule("Detect Nade Collision")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Distance Between(Event Player.StartPos + Event Player.StartVelocity * Event Player.NadeTimerChase + Vector(0, -4.900, 0)
			* Event Player.NadeTimerChase * Event Player.NadeTimerChase, Ray Cast Hit Position(
			Event Player.StartPos + Event Player.StartVelocity * Event Player.NadeTimerChase + Vector(0, -4.900, 0)
			* Event Player.NadeTimerChase * Event Player.NadeTimerChase,
			Event Player.StartPos + Event Player.StartVelocity * Event Player.NadeTimerChase + Vector(0, -4.900, 0)
			* Event Player.NadeTimerChase * Event Player.NadeTimerChase + Normalize(
			Event Player.StartVelocity + Event Player.NadeTimerChase * Vector(0, -9.800, 0)), All Players(All Teams), Event Player, True))
			< 0.990;
	}

	actions
	{
		Stop Chasing Player Variable(Event Player, NadeTimerChase);
		Small Message(Event Player, Custom String("Press {1} + {2} to go next checkpoint. Fly Time: {0} sec", Event Player.NadeTimerChase,
			Input Binding String(Button(Interact)), Input Binding String(Button(Reload))));
		Wait(1, Ignore Condition);
		Stop Camera(Event Player);
		If(Global.PioneerMode && Event Player == Host Player);
			Modify Global Variable(CPLocation, Append To Array, Position Of(Event Player));
			Modify Global Variable(CPDestination, Append To Array,
				Event Player.StartPos + Event Player.StartVelocity * Event Player.NadeTimerChase + Vector(0, -4.900, 0)
				* Event Player.NadeTimerChase * Event Player.NadeTimerChase);
			Modify Global Variable(CPFacing, Append To Array, Vector(Horizontal Angle From Direction(Facing Direction Of(Event Player)),
				Vertical Angle From Direction(Facing Direction Of(Event Player)), 0));
			Modify Global Variable(CPHintText, Append To Array, Custom String(""));
			Modify Global Variable(CPObjectiveText, Append To Array, Custom String(""));
			Modify Global Variable(CPHintEffectPosition, Append To Array, Null);
			Modify Global Variable(CPScoped, Append To Array, False);
			Global.PioneerMode = False;
			Small Message(Host Player, Custom String("New CP added to map data. Pioneer mode now turned off."));
		End;
	}
}

disabled rule("Key Bindings")
{
	event
	{
		Ongoing - Global;
	}
}

rule("Reload - Reset Camera + Position, Melee = Reset Camera")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		(Event Player.NadeTimerChase > 9 || Is Button Held(Event Player, Button(Reload)) || Is Button Held(Event Player, Button(Melee)))
			== True;
		Is Button Held(Event Player, Button(Interact)) != True;
	}

	actions
	{
		Stop Chasing Player Variable(Event Player, NadeTimerChase);
		Stop Camera(Event Player);
		If(!Is Button Held(Event Player, Button(Melee)));
			Call Subroutine(RespawnPlayer);
		End;
	}
}

rule("Interact + Reload - Goto Next CP")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Button(Interact)) == True;
		Is Button Held(Event Player, Button(Reload)) == True;
	}

	actions
	{
		Event Player.CP = (Event Player.CP + 1) % Count Of(Global.CPLocation);
		Call Subroutine(RespawnPlayer);
	}
}

rule("Melee + Ultimate - Toggle Chase Cam")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Button(Melee)) == True;
		Is Button Held(Event Player, Button(Ultimate)) == True;
	}

	actions
	{
		Event Player.ChaseCamOn = !Event Player.ChaseCamOn;
		Small Message(Event Player, Event Player.ChaseCamOn ? Custom String("Chase Cam On") : Custom String("Chase Cam Off"));
	}
}

rule("Ultimate Status - Set Destination point")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Communicating(Event Player, Ultimate Status) == True;
	}

	actions
	{
		Event Player.Destination = Position Of(Event Player);
		Small Message(Event Player, Custom String("Destination Point Set: {0}", Position Of(Event Player)));
	}
}

rule("Interact + Ultimate - Compute throw to destination point - THANKS https://www.youtube.com/watch?v=bqYtNrhdDAY")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Button(Ultimate)) == True;
		Is Button Held(Event Player, Button(Interact)) == True;
	}

	actions
	{
		"horizontal distance"
		Event Player.M = Distance Between(Event Player.Destination * Vector(1, 0, 1), Position Of(Event Player) * Vector(1, 0, 1));
		"vert distance"
		Event Player.O = Y Component Of(Position Of(Event Player)) - Y Component Of(Event Player.Destination);
		"Formula from https://youtu.be/bqYtNrhdDAY?t=818"
		Event Player.P = (Arccosine In Degrees((9.800 * Event Player.M * Event Player.M / 900 - Event Player.O) / Distance Between(
			Event Player.Destination, Position Of(Event Player))) + Arctangent In Degrees(Event Player.M, Event Player.O)) / -2;
		Set Facing(Event Player, Direction From Angles(Horizontal Angle From Direction(Direction Towards(Position Of(Event Player),
			Event Player.Destination)), Event Player.P), To World);
	}
}

rule("Ana Sleep Dart Teleport")
{
	event
	{
		Ongoing - Each Player;
		All;
		Ana;
	}

	conditions
	{
		Is Using Ability 1(Event Player) == True;
	}

	actions
	{
		Stop Camera(Event Player);
		"The duration before Ana fires sleep dart"
		Wait(0.300, Ignore Condition);
		Event Player.AnaSleepDartTeleLocation = Ray Cast Hit Position(Eye Position(Event Player), Eye Position(Event Player)
			+ Facing Direction Of(Event Player) * 60, All Players(All Teams), Event Player, True);
		"Shorten ray cast to avoid unintended tele through surfaces"
		Event Player.AnaSleepDartTeleLocation -= Direction Towards(Eye Position(Event Player), Event Player.AnaSleepDartTeleLocation)
			* 0.500;
		"Sleep dart travel time"
		Wait(Distance Between(Eye Position(Event Player), Event Player.AnaSleepDartTeleLocation) / 60, Ignore Condition);
		Teleport(Event Player, Event Player.AnaSleepDartTeleLocation);
	}
}

disabled rule("Subroutines")
{
	event
	{
		Ongoing - Global;
	}
}

rule("Sub0 - RespawnPlayer")
{
	event
	{
		Subroutine;
		RespawnPlayer;
	}

	actions
	{
		Stop Camera(Event Player);
		Teleport(Event Player, Global.CPLocation[Event Player.CP]);
		If(Magnitude Of(Global.CPFacing[Event Player.CP]) > 2);
			"Horiz/Vertical angle facing direction format "
			Set Facing(Event Player, Direction From Angles(X Component Of(Global.CPFacing[Event Player.CP]), Y Component Of(
				Global.CPFacing[Event Player.CP])), To World);
		Else;
			"Vector facing direction format "
			Set Facing(Event Player, Global.CPFacing[Event Player.CP], To World);
		End;
		disabled Small Message(Event Player, Custom String("{0}", Global.CPHintText[Event Player.CP]));
		If(Is Firing Secondary(Event Player) != Global.CPScoped[Event Player.CP]);
			Press Button(Event Player, Button(Secondary Fire));
		End;
	}
}

disabled rule("Find Angles from Start and Destination Positions")
{
	event
	{
		Ongoing - Global;
	}
}

disabled rule("Pioneer Mode Stuff")
{
	event
	{
		Ongoing - Global;
	}
}

rule("Interact + Hello - Toggle Dev Mode [HOST ONLY]")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Event Player == Host Player;
		Is Button Held(Event Player, Button(Interact)) == True;
		Is Communicating(Event Player, Hello) == True;
	}

	actions
	{
		Global.DevModeEngaged = !Global.DevModeEngaged;
		Wait(0.100, Ignore Condition);
		Small Message(Event Player, Global.DevModeEngaged ? Custom String("Dev Mode Engaged") : Custom String("Dev Mode Disengaged"));
	}
}

rule("Need Healing + Dev Mode On - Toggle Pioneer Mode [HOST ONLY]")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Global.DevModeEngaged == True;
		Is Communicating(Host Player, Need Healing) == True;
	}

	actions
	{
		Global.PioneerMode = !Global.PioneerMode;
		Wait(0.100, Ignore Condition);
		Small Message(Host Player, Global.PioneerMode ? Custom String("Pioneer Mode Engaged") : Custom String("Pioneer Mode Disengaged"));
	}
}

rule("Emote + Melee + Dev Mode On - Delete Last CP [HOST ONLY]")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Global.DevModeEngaged == True;
		Is Communicating Any Emote(Host Player) == True;
		Is Button Held(Host Player, Button(Melee)) == True;
	}

	actions
	{
		Small Message(All Players(All Teams), Custom String("Deleting last CP"));
		Modify Global Variable(CPLocation, Remove From Array By Index, Count Of(Global.CPLocation) - 1);
		Modify Global Variable(CPDestination, Remove From Array By Index, Count Of(Global.CPDestination) - 1);
		Modify Global Variable(CPFacing, Remove From Array By Index, Count Of(Global.CPFacing) - 1);
		Modify Global Variable(CPHintText, Remove From Array By Index, Count Of(Global.CPHintText) - 1);
		Modify Global Variable(CPHintEffectPosition, Remove From Array By Index, Count Of(Global.CPHintEffectPosition) - 1);
		Modify Global Variable(CPScoped, Remove From Array By Index, Count Of(Global.CPScoped) - 1);
		Modify Global Variable(CPObjectiveText, Remove From Array By Index, Count Of(Global.CPObjectiveText) - 1);
	}
}